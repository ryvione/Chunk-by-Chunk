plugins {
    id 'dev.architectury.loom' version '1.13.467'
    id 'maven-publish'
}

version = rootProject.version
group = rootProject.group
archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

repositories {
    maven {
        name = "Terraformers SMC"
        url = "https://maven.terraformersmc.com/releases"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings() // THIS STAYS - IT'S THE KEY!
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // Common module
    implementation(project(path: ':Common', configuration: 'namedElements'))

    // ModMenu
    modImplementation "com.terraformersmc:modmenu:11.0.2"

    // JEI for Fabric
    modCompileOnly "mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}"
    modRuntimeOnly "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"

    // Annotations
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}

loom {
    mixin {
        defaultRefmapName = "${mod_name}-fabric-${minecraft_version}-refmap.json"
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(JavaCompile).configureEach {
    source(project(':Common').sourceSets.main.allSource)
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : archiveVersion.get(),
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : archiveVersion.get(),
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
        ])
    }
}

remapJar {
    inputFile = jar.archiveFile
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}