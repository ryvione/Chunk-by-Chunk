plugins {
    id 'net.neoforged.moddev' version '2.0.28-beta'
    id 'maven-publish'
}

version = rootProject.version
group = rootProject.group

base {
    archivesName = "${mod_name}-neoforge-${minecraft_version}"
}

repositories {
    maven {
        name = 'BlameJared Maven (JEI)'
        url = 'https://maven.blamejared.com'
    }
}

neoForge {
    version = '21.1.77'
    parchment {
        minecraftVersion = minecraft_version
        mappingsVersion = '2024.11.17'
    }
    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    srcDir project(':Common').file('src/main/resources')
}

dependencies {
    implementation project(':Common')
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
}

tasks.withType(JavaCompile).configureEach {
    source(project(':Common').sourceSets.main.allSource)
}

tasks.named('processResources', ProcessResources).configure {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    def replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: "[${minecraft_version},1.22)",
            neoforge_version       : '21.1.219',
            neoforge_version_range : '[21.1,)',
            loader_version_range   : '[4,)',
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : 'MIT License',
            mod_version            : version,
            mod_authors            : mod_author,
            mod_author             : mod_author,
            mod_description        : 'This mod starts the overworld with a single chunk, but provides the tools to expand the world one chunk at a time.'
    ]
    inputs.properties replaceProperties

    from(project(':Common').sourceSets.main.resources) {
        exclude 'META-INF/neoforge.mods.toml'
        exclude 'fabric.mod.json'
    }

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : version,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.base.archivesName.get()
            version project.version
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}
