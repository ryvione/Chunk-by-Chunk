plugins {
    id 'java'
}
subprojects {
    apply plugin: 'java'
    group = 'com.ryvione'
    version = rootProject.version
    java {
        toolchain.languageVersion = JavaLanguageVersion.of(21)
        withSourcesJar()
        withJavadocJar()
    }
    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 21
    }
    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = 'Progwml6 Maven (JEI)'
            url = 'https://dvs1.progwml6.com/files/maven/'
        }
    }
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}
task copyToReleases(type: Copy) {
    dependsOn ':Fabric:build', ':NeoForge:build'
    from files('Fabric/build/libs', 'NeoForge/build/libs')
    include "*.jar"
    exclude "*-sources.jar"
    exclude "*-javadoc.jar"
    into file("$rootDir/Releases")
    doLast {
        println "Copied jars to Releases/"
    }
}